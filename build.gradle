plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '7.2.1' apply false
}

allprojects {
    group = 'ua.studying'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'jacoco'

    spotless {
        java {
            googleJavaFormat()
            removeUnusedImports()
            trimTrailingWhitespace()
            removeWildcardImports()
            endWithNewline()
        }
    }

    test {
        finalizedBy jacocoTestReport
        useJUnitPlatform()

        testLogging {
            events 'passed', 'skipped', 'failed'
            showStandardStreams = true
            exceptionFormat = 'short'
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = false
            csv.required = false
            html.required = false
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        testCompileOnly {
            extendsFrom testAnnotationProcessor
        }
    }

    bootJar {
        archiveFileName.set("${rootProject.name}${version}.jar")
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'

        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}
